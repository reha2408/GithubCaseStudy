apply plugin: 'com.dicedmelon.gradle.jacoco-android'

def excludedFiles = [
        'jdk.internal.*',
        // data binding
        'android/databinding/**/*.class',
        '**/android/databinding/*Binding.class',
        '**/android/databinding/*',
        '**/androidx/databinding/*',
        '**/BR.*',
        // android
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*.*',
        // kotlin
        '**/*MapperImpl*.*',
        '**/*$ViewInjector*.*',
        '**/*$ViewBinder*.*',
        '**/BuildConfig.*',
        '**/*Component*.*',
        '**/*BR*.*',
        '**/Manifest*.*',
        '**/*$Lambda$*.*',
        '**/*Companion*.*',
        '**/*Module*.*',
        '**/*Dagger*.*',
        '**/*Hilt*.*',
        '**/*MembersInjector*.*',
        '**/*_MembersInjector.class',
        '**/*_Factory*.*',
        '**/*_Provide*Factory*.*',
        '**/*Extensions*.*',
        // sealed and data classes
        '**/*$Result.*',
        '**/*$Result$*.*',
        // adapters generated by moshi
        '**/*JsonAdapter.*',
]

jacoco {
    toolVersion = "0.8.7"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes += excludedFiles // Allows it to run on Java 11
}

android {
    // Gradle automatically adds 'android.test.runner' as a dependency.
    useLibrary 'android.test.runner'

    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }
    testOptions {
        /*execution 'ANDROIDX_TEST_ORCHESTRATOR'*/
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }
    dependencies {
        // Required -- JUnit 4 framework
        testImplementation 'junit:junit:4.13.2'
        // Optional -- Robolectric environment
        testImplementation 'androidx.test:core:1.4.0'
        testImplementation 'org.robolectric:robolectric:4.7.3'

        // Core library
        androidTestImplementation 'androidx.test:core:1.4.0'

        // AndroidJUnitRunner and JUnit Rules
        androidTestImplementation 'androidx.test:runner:1.4.0'
        androidTestImplementation 'androidx.test:rules:1.4.0'

        // Assertions
        androidTestImplementation 'androidx.test.ext:junit:1.1.3'
        testImplementation 'androidx.test.ext:junit:1.1.3'
        androidTestImplementation 'androidx.test.ext:truth:1.4.0'
        testImplementation "com.google.truth:truth:1.1.3"

        // Optional -- Mockito framework
        testImplementation 'org.mockito:mockito-core:3.5.11'
        testImplementation 'org.mockito:mockito-inline:3.5.11'
        testImplementation 'org.hamcrest:hamcrest:2.2'
        testImplementation "androidx.arch.core:core-testing:2.1.0"
        testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-alpha03@jar"

        implementation "androidx.fragment:fragment-ktx:$fragmentKtxVersion"
        debugImplementation "androidx.fragment:fragment-testing:$fragmentKtxVersion"
        androidTestImplementation "androidx.fragment:fragment-testing:$fragmentKtxVersion"

        androidTestImplementation 'androidx.test.ext:junit:1.1.3'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    }
}
