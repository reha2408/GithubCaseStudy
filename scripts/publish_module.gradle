apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                // artifact javadocJar // use dokka plugin for kotlin.

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Android Base App'
                    url = 'https://github.com/reha2408/GithubCaseStudy'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'reha2408'
                            name = 'Reha TopuzoÄŸlu'
                            email = 'reha2408@gmail.com'
                        }
                        // Add all other devs here...
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/reha2408/GithubCaseStudy.git'
                        developerConnection = 'scm:git:ssh://github.com/reha2408/GithubCaseStudy.git'
                        url = 'https://github.com/reha2408/GithubCaseStudy/tree/main'
                    }
                }
            }
        }
    }
    tasks.create("publishRtx") {
        description = "Publish rtx-framework"
        group = "Publish rtx-framework"
        dependsOn ":rtxframework:ktlint"
        dependsOn ":rtxframework:detekt"
        dependsOn ":rtxframework:publishReleasePublicationToSonatypeRepository"
        dependsOn ":closeAndReleaseSonatypeStagingRepository"
        tasks.findByPath(":rtxframework:detekt").mustRunAfter(":rtxframework:ktlint")
        tasks.findByPath(":rtxframework:publishReleasePublicationToSonatypeRepository").mustRunAfter(":rtxframework:detekt")
        tasks.findByPath(":closeAndReleaseSonatypeStagingRepository").mustRunAfter(":rtxframework:publishReleasePublicationToSonatypeRepository")
    }
    tasks.create("codeQualityRtx") {
        description = "Code Quality rtx-framework"
        group = "Publish rtx-framework"
        dependsOn ":rtxframework:ktlint"
        dependsOn ":rtxframework:detekt"
    }
    tasks.create("unitTestRtx") {
        description = "Unit Test rtx-framework"
        group = "Publish rtx-framework"
        dependsOn ":rtxframework:testDebugUnitTest"
        doLast {
            exec {
                commandLine 'open', "$rootProject.projectDir/rtxframework/build/reports/tests/testDebugUnitTest/index.html"
            }
        }
    }
    tasks.create("codeCoverageReportRtx") {
        description = "Code Coverage rtx-framework"
        group = "Publish rtx-framework"
        dependsOn ":rtxframework:jacocoTestReport"
        doLast {
            exec {
                commandLine 'open', "$rootProject.projectDir/rtxframework/build/jacoco/jacocoHtml/index.html"
            }
        }
    }

    tasks.create("codeQualityRtxWrapperApp") {
        description = "Code Quality rtx-framework"
        group = "Publish rtx-framework"
        dependsOn ":app:ktlint"
        dependsOn ":app:detekt"
    }
    tasks.create("unitTestRtxWrapperApp") {
        description = "Unit Test rtx-framework"
        group = "Publish rtx-framework"
        dependsOn ":app:testDebugUnitTest"
        doLast {
            exec {
                commandLine 'open', "$rootProject.projectDir/app/build/reports/tests/testDebugUnitTest/index.html"
            }
        }
    }
    tasks.create("codeCoverageReportRtxWrapperApp") {
        description = "Code Coverage Test rtx-framework"
        group = "Publish rtx-framework"
        dependsOn ":app:jacocoTestReport"
        doLast {
            exec {
                commandLine 'open', "$rootProject.projectDir/app/build/jacoco/jacocoHtml/index.html"
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}